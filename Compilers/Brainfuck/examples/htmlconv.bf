
;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
 ;this program converts ASCII_characters ae oe ue Ae Oe Ue (german 'umlauts')
 ;gt lt ss to valid html_code (it's been already tested with bfi)
 ;(erdalkaraca@t_online_de)

 >++++++++++[>+++++++++++++<-]>++           ae :var02
 >>++++++++++++[<++++++++++++>-]<++++       oe :var03
 >>+++++++++++[<+++++++++++>-]<++++++++     ue :var04
 >>++++++++++++[<++++++++++++>-]<--         Ae :var05
 >>++++++++++++[<+++++++++++++>-]<---       Oe :var06
 >>++++++++++++[<+++++++++++++>-]<--        Ue :var07
 >>++++++++[<++++++++>-]<--                 gt :var08
 >>++++++++[<+++++++>-]<++++                lt :var09

 ;to add more variables_ use elements above 17

 >>>>>>>>>>+++++++++++++++
           [<+++++++++++++++>-]<            ss :var10
                                18
 <<<<<<<<<

>+10
 [>,11
   [     ;IF x == var02 THEN PRINT "&auml;"
    <<<<<<<<<2[<1+<0+>>-]<[>+<-]
    >>>>>>>>>>
    [<<<<<<<<<<1+<0->>>>>>>>>>>11-]
    <<<<<<<<<<1[>>>>>>>>>>11+<<<<<<<<<<1-]
    >>>>>>>>>>
    >12+>13+<<<<<<<<<<<<<0[>>>>>>>>>>>>-<<<<<<<<<<<<0[-]]
    >>>>>>>>>>>11
    >12[>>++++++[>++++++<-]>++.
        <+++++++[>++++++++<-]>+++.
        <++++[>+++++<-]>.
        <++[>----<-]>.
        <+[>-<-]>.
        <+++++++[>-------<-]>15.[-]+
        <<13!-<<11[-]
        >12-
       ]
    >13[    ;ELSE IF x == var03 THEN PRINT "&ouml;"
        <<<<<<<<<<3[<<+<+>>>-]<<1[>>3+<<-]
        >>>>>>>>>>
        [<<<<<<<<<<+<0->>>>>>>>>>>11-]
        <<<<<<<<<<1[>>>>>>>>>>11+<<<<<<<<<<1-]
        >>>>>>>>>>
        >12+>>14+<<<<<<<<<<<<<<0[>>>>>>>>>>>>12-<<<<<<<<<<<<0[-]]
        >>>>>>>>>>>11
        >12[>>>>16++++++[<++++++>-]<++.
            >++++++++[<+++++++++>-]<+.
            >++[<+++>-]<.
            >++[<---->-]<.
            >+[<->-]<.
            >+++++++[<------->-]<15.[-]+
            <14!-<<<11[-]
            >12-
           ]
        >13-
       ]
    >14[    ;ELSE IF x == var04 THEN PRINT "&uuml;"
        <<<<<<<<<<4[<<<1+<0+>>>>4-]<<<1[>>>+<<<-]
        >>>>>>>>>>
        [<<<<<<<<<<+<0->>>>>>>>>>>11-]
        <<<<<<<<<<[>>>>>>>>>>+<<<<<<<<<<-]
        >>>>>>>>>>
        >12+>13+<<<<<<<<<<<<<0[>>>>>>>>>>>>-<<<<<<<<<<<<[-]]
        >>>>>>>>>>>
        >12[>>>>16++++++[<++++++>-]<++.
            >++++++++[<+++++++++>-]<+++++++..
            >++[<---->-]<.
            >+[<->-]<.
            >+++++++[<------->-]<15.[-]+
            <<13!-<<11[-]
            >12-
           ]
        >>14-
       ]
    <13[    ;ELSE IF x == var05 THEN PRINT "&Auml;"
        <<<<<<<<[<<<<+<+>>>>>-]<<<<[>>>>+<<<<-]
        >>>>>>>>>>11
        [<<<<<<<<<<+<0->>>>>>>>>>>11-]
        <<<<<<<<<<1[>>>>>>>>>>+<<<<<<<<<<-]
        >>>>>>>>>>
        >12+>>14+<<<<<<<<<<<<<<[>>>>>>>>>>>>-<<<<<<<<<<<<[-]]
        >>>>>>>>>>>11
        >12[>>>>++++++[<++++++>-]<++.
            >+++++[<+++++>-]<++.
            >+++++++[<+++++++>-]<+++.
            >++[<---->-]<.
            >+[<->-]<.
            >+++++++[<------->-]<15.[-]+
            <14!-<<<11[-]
            >12-
           ]
        >13-
       ]
    >14[    ;ELSE IF x == var06 THEN PRINT "&Ouml;"
        <<<<<<<<[<<<<<+<+>>>>>>-]<<<<<[>>>>>+<<<<<-]
        >>>>>>>>>>
        [<<<<<<<<<<+<0->>>>>>>>>>>11-]
        <<<<<<<<<<[>>>>>>>>>>+<<<<<<<<<<-]
        >>>>>>>>>>
        >12+>13+<<<<<<<<<<<<<[>>>>>>>>>>>>-<<<<<<<<<<<<[-]]
        >>>>>>>>>>>11
        >12[>>>>++++++[<++++++>-]<++.
            >++++++[<++++++>-]<+++++.
            >++++++[<++++++>-]<++.
            >++[<---->-]<.
            >+[<->-]<.
            >+++++++[<------->-]<15.[-]+
            <<13!-<<11[-]
            >12-
           ]
        >>14-
       ]
    <13[    ;ELSE IF x == var07 THEN PRINT "&Uuml;"
        <<<<<<[<<<<<<+<+>>>>>>>-]<<<<<<[>>>>>>+<<<<<<-]
        >>>>>>>>>>
        [<<<<<<<<<<+<0->>>>>>>>>>>11-]
        <<<<<<<<<<[>>>>>>>>>>+<<<<<<<<<<-]
        >>>>>>>>>>
        >12+>>14+<<<<<<<<<<<<<<[>>>>>>>>>>>>-<<<<<<<<<<<<[-]]
        >>>>>>>>>>>11
        >12[>>>>++++++[<++++++>-]<++.
            >++++++[<+++++++>-]<+++++.
            >+++++[<++++++>-]<++.
            >++[<---->-]<.
            >+[<->-]<.
            >+++++++[<------->-]<.[-]+
            <!-<<<[-]
            >-
           ]
        >13-
       ]
    >14[    ;ELSE IF x == var08 THEN PRINT "&gt;"
        <<<<<<[<<<<<<<+<+>>>>>>>>-]<<<<<<<[>>>>>>>+<<<<<<<-]
        >>>>>>>>>>
        [<<<<<<<<<<+<0->>>>>>>>>>>11-]
        <<<<<<<<<<[>>>>>>>>>>+<<<<<<<<<<-]
        >>>>>>>>>>
        >+>13+<<<<<<<<<<<<<[>>>>>>>>>>>>-<<<<<<<<<<<<[-]]
        >>>>>>>>>>>
        >12[>>>>++++++[<++++++>-]<++.
            >++++++++[<++++++++>-]<+.
            >+++[<++++>-]<+.
            >+++++++[<-------->-]<-.[-]+
            <<!-<<[-]
            >-
           ]
        >>-
       ]
    <13[    ;ELSE IF x == var09 THEN PRINT "&lt;"
        <<<<[<<<<<<<<+<+>>>>>>>>>-]<<<<<<<<[>>>>>>>>+<<<<<<<<-]
        >>>>>>>>>>
        [<<<<<<<<<<+<0->>>>>>>>>>>11-]
        <<<<<<<<<<[>>>>>>>>>>+<<<<<<<<<<-]
        >>>>>>>>>>
        >+>>14+<<<<<<<<<<<<<<[>>>>>>>>>>>>-<<<<<<<<<<<<[-]]
        >>>>>>>>>>>
        >12[>>>>++++++[<++++++>-]<++.
            >++++++++[<++++++++>-]<++++++.
            >++[<++++>-]<.
            >+++++++[<-------->-]<-.[-]+
            <!-<<<[-]
            >-
           ]
        >-
       ]
    >14[    ;ELSE IF x == var10 THEN PRINT "&szlig;"
        >>>>18[<<<<<<<<<<<<<<<<<+<+>>>>>>>>>>>>>>>>>>-]
          <<<<<<<<<<<<<<<<<[>>>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<-]
        >>>>>>>>>>
        [<<<<<<<<<<+<->>>>>>>>>>>-]
        <<<<<<<<<<[>>>>>>>>>>+<<<<<<<<<<-]
        >>>>>>>>>>
        >+>13+<<<<<<<<<<<<<[>>>>>>>>>>>>-<<<<<<<<<<<<[-]]
        >>>>>>>>>>>
        >12[>>>>++++++[<++++++>-]<++.
            >++++++++[<+++++++++>-]<+++++.
            >++[<+++>-]<+.
            >+++[<---->-]<--.
            >+[<--->-]<.
            >+[<-->-]<.
            >++++++[<------->-]<--.[-]+
            <<!-<<[-]
            >-
           ]
        >>-
       ]
    <13[    ;ELSE PRINT x
        <<.[-]
        >>>>[-]+
        <<-
       ]
    <<11
   ]
  >>>>>>+<<[<<<<<[-]+>>>>>->>-<<]>>[<<<<<<<[-]>>>>>>>-]
  <<<<<<<
 ]

;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~